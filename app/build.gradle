apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {

    final URL_PROD = "http://www.google.com"
    final URL_QA = "http://www.google.com"

    final API_URL_PROD = "http://api.rottentomatoes.com/api/public/v1.0"
    final API_URL_QA = "http://api.rottentomatoes.com/api/public/v1.0"

    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.gf.movie.reminder"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            if (System.getenv("KEYSTORE")) {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            } else {
//                Properties props = new Properties()
//                props.load(new FileInputStream(file("gradle.properties")))
//
//                storeFile file(props['release_keystore'])
//                storePassword props['release_keystore.password']
//                keyAlias props['release_keyAlias']
//                keyPassword props['release_keyPassword']
                storeFile file('debug.keystore')
            }
        }

        debug {
            storeFile file('debug.keystore')
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug-${gitSha}"
            signingConfig signingConfigs.debug

            buildConfigField "String", "API_URL", "\"" + API_URL_QA + "\""
            buildConfigField "String", "URL", "\"" + URL_QA + "\""
            buildConfigField "boolean", "TRACK_APP_INSTALLS", "true"
            if (project.hasProperty('endpoint')) {
                buildConfigField "String", "DEFAULT_ENDPOINT", "\"" + endpoint + "\""
            } else {
                buildConfigField "String", "DEFAULT_ENDPOINT", "\"" + "mock" + "\""
            }
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "API_URL", "\"" + API_URL_PROD + "\""
            buildConfigField "String", "URL", "\"" + URL_PROD + "\""
            buildConfigField "boolean", "TRACK_APP_INSTALLS", "true"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError true

        htmlReport true

        checkAllWarnings true
        warningsAsErrors true
    }
}

configurations {
    all*.exclude group: 'com.google.android', module: 'support-v4'
    all*.exclude group: 'com.google.android', module: 'appcompat-v7'
    all*.exclude group: 'com.google.android', module: 'support-v13'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:support-v13:21.0.2'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.retrofit:retrofit:1.7.1'
    compile 'com.squareup.okhttp:okhttp:2.1.0-RC1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0-RC1'
    compile 'info.metadude.android:typed-preferences:1.1.0'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'io.realm:realm-android:0.76.0'
    compile 'com.github.ksoichiro:android-observablescrollview:1.4.0'
    compile 'com.github.pedrovgs:draggablepanel:1.5@aar'

    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    debugCompile 'com.squareup.retrofit:retrofit-mock:1.7.1'
}